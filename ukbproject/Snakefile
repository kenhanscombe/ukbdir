import os
import glob
import re
import pandas as pd

from pathlib import Path

# --config-file passed to snakemake on command line

project_dir = config['project_dir']
pkg_dir = config['pkg_dir']

resources = Path(project_dir).parent / 'resources'
bin_dir = Path(project_dir).parent / 'bin'

ukbunpack = f'{bin_dir}/ukbunpack'
ukbconv = f'{bin_dir}/ukbconv'

dataset_ids = [re.sub(r'^.*ukb|\.enc', '', d) for d in glob.glob(f'{project_dir}/raw/*.enc')]

rule all:
    input:
        expand(f'{project_dir}/raw/ukb{{id}}.enc_ukb', id=dataset_ids),
        expand(f'{project_dir}/phenotypes/ukb{{id}}.csv', id=dataset_ids),
        expand(f'{project_dir}/phenotypes/ukb{{id}}.html', id=dataset_ids),
        expand(f'{project_dir}/phenotypes/ukb{{id}}_field_finder.txt', id=dataset_ids)


rule ukbunpack_decrypt:
    input:
        enc = f'{project_dir}/raw/ukb{{id}}.enc',
        key = f'{project_dir}/raw/ukb{{id}}.key'
    output:
        f'{project_dir}/raw/ukb{{id}}.enc_ukb'
    log: f'{project_dir}/logs/ukbunpack_decrypt_{{id}}.log'
    resources: mem=12000, time=30
    shell:
        '''
        {ukbunpack} {input.enc} {input.key} &> {log}
        '''


rule ukbconv_convert:
    input:
        f'{project_dir}/raw/ukb{{id}}.enc_ukb'
    output:
        csv = f'{project_dir}/phenotypes/ukb{{id}}.csv',
        html = f'{project_dir}/phenotypes/ukb{{id}}.html'
    log: f'{project_dir}/logs/ukbconv_convert_{{id}}.log'
    resources: mem=12000, time=60*48
    params:
        out = f'{project_dir}/phenotypes/ukb{{id}}',
        enc = f'{resources}/encoding.ukb'
    shell:
        '''
        {ukbconv} {input} csv -o{params.out} -e{params.enc} &> {log}
        {ukbconv} {input} docs -o{params.out} -e{params.enc} >> {log} 2>&1
        '''


rule munge_ukb_html:
    input:
        html = f'{project_dir}/phenotypes/ukb{{id}}.html'
    output:
        finder = f'{project_dir}/phenotypes/ukb{{id}}_field_finder.txt'
    log: f'{project_dir}/logs/munge_ukb_html_{{id}}.log'
    resources: mem=12000, time=60*4
    params:
        basket = 'ukb{id}',
        out_dir = f'{project_dir}/phenotypes/'
    envmodules:
        "devtools/anaconda/2019.3-python3.7.3"
    shell:
        f'''
        {pkg_dir}/munge.py \
            --html {{input.html}} \
            --basket {{params.basket}} \
            --out-dir {{params.out_dir}} \
            &> {{log}}
        '''
